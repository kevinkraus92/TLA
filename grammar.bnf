<S> ::=  "EMPEZAR" <code> "TERMINAR"

<code> ::= "" | <instruction> <code> | <control_sequence> <code>

<instruction> ::= <declaration> <asign>"." | <declaration>"." | <print>"." | <var_name> <asign>"." | <var_name> "aumentar" | <var_name> "decrementar" | "entrada" "¿" <var_name> "?" "."

<declaration> ::= <type> <var_name>

<var_name> ::= <letter> | <var_name> <letter>

<string> ::= <letter> <string> | <integer> <string> | <symbol> <string> | "" 

<letter> ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | ""

 <digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

 <symbol> ::=  "|" | " " | "-" | "!" | "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":" | ";" | "<" | "=" | ">" | "?" | "@" | "[" | "\" | "]" | "^" | "_" | "`" | "{" | "|" | "}" | "~"

<type> ::= "entero"

<asign> ::= "vale" <expression>

<integer> ::= <digit> | <integer> <digit>

<print> ::= "imprime" "¿" "~" <string> "?" | "imprime" "¿" <var_name> "?"

<control_sequence> ::= <if> | <loop>

<if> ::= "si" "¿" <boolean_expression> "?" <open_block> <code> <close_block> |
 "si" "¿" <boolean_expression> "?" <open_block> <code> <close_block> "sino" <open_block> <code> <close_block>

<loop> ::= "hacer" <code> "mientras" <boolean_expresion>  | "mientras" <boolean_expression> <open_block> <code> <close_block>

<open_block> ::= "<"

<close_block> ::= ">"

<boolean_expression> ::= <boolean_expression> "o" <boolean_expression> | <boolean_term>

<boolean_term> ::= <boolean_term> "y" <boolean_term> | <boolean_factor>

<boolean_factor> ::= "(" <boolean_expression> ")" | "no" <boolean_factor> | <boolean>

<boolean> ::= true | false | <comparation>

<comparation> ::=  <expression> <compare_operator> <expression>

<expression> ::= "(" <expression> "+" <term> ")" | "(" <expression> "-" <term> ")" | <term>

<term> ::= "(" <term> "*" <factor> ")" | "(" <term> "/" <factor> ")" | <factor>

<factor> ::= <var_name> | <integer>

<compare_operator> ::= " menor " | " mayor " | " igual " |" distinto " | " menor igual " | " mayor igual "